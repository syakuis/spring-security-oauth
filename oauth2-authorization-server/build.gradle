apply plugin: "java"
apply plugin: "application"
apply plugin: "org.asciidoctor.jvm.convert"

configurations {
    asciidoctorExt
}

dependencies {
    implementation project(":core")
    testImplementation project(":core-test")
    implementation project(":account-core")
    implementation project(":client-registration-core")

    implementation 'mysql:mysql-connector-java:8.0.29'

    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"

    // spring security oauth2 and jwt
    implementation "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.6.8"
    implementation "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOAuth2Version}"

    implementation "org.springframework.security:spring-security-oauth2-jose"
    testImplementation "org.springframework.security:spring-security-test"

    // oauth2 resource server
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"

    // jwt utils
    implementation 'org.springframework.security:spring-security-jwt:1.1.1.RELEASE'
    implementation "io.jsonwebtoken:jjwt:0.9.1"
    implementation 'com.nimbusds:oauth2-oidc-sdk:9.37.2'

    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation 'org.apache.commons:commons-pool2:2.11.1'

    testImplementation "org.springframework.boot:spring-boot-starter-webflux"

    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.77.Final:osx-aarch_64'
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty 'spring.profiles.active', 'test'
}

application {
    mainClass = "io.github.syakuis.oauth2.AuthorizationApplication"
}

bootJar {
    enabled = true
    launchScript()
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into "BOOT-INF/classes/static/api-docs"
    }
}

jar {
    enabled = false
}