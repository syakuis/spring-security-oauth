apply plugin: "java"
apply plugin: "application"
apply plugin: "org.asciidoctor.jvm.convert"

configurations {
    asciidoctorExt
}

dependencies {
    implementation project(":core")
    testImplementation project(":core-test")
    implementation project(":client-registration-core")
    implementation project(":oauth2-resource-server")

    implementation 'mysql:mysql-connector-java:8.0.29'

    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"

    testImplementation "org.springframework.security:spring-security-test"
    // resource-server
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation 'com.nimbusds:oauth2-oidc-sdk:9.37.2'

    testImplementation "com.github.tomakehurst:wiremock-jre8:2.33.2"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner"
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty 'spring.profiles.active', 'test'
}

application {
    mainClass = "io.github.syakuis.oauth2.ClientRegistrationApplication"
}

bootJar {
    enabled = true
    launchScript()
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into "BOOT-INF/classes/static/api-docs"
    }
}

jar {
    enabled = false
}