apply plugin: "java"
apply plugin: "application"
apply plugin: "org.asciidoctor.jvm.convert"

configurations {
    asciidoctorExt
}

dependencies {
    implementation project(":core")
    testImplementation project(":core-test")
    implementation project(":client-registration-core")

    implementation 'mysql:mysql-connector-java:8.0.29'

    implementation "org.springframework.security:spring-security-core"
    testImplementation "org.springframework.security:spring-security-test"

    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty 'spring.profiles.active', 'test'
}

application {
    mainClass = "io.github.syakuis.oauth2.ClientRegistrationApplication"
}

bootJar {
    enabled = true
    launchScript()
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into "BOOT-INF/classes/static/api-docs"
    }
}

jar {
    enabled = false
}